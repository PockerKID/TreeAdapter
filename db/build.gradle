plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.greenrobot.greendao'
}

android {
    namespace 'com.z.db'
    compileSdk compile_sdk_version

    defaultConfig {
        minSdk min_sdk_version
        targetSdk target_sdk_version
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/MANIFEST.MF'

        pickFirst '**/*.so'
    }
    compileOptions {
        sourceCompatibility java_version
        targetCompatibility java_version
    }
    kotlinOptions {
        jvmTarget = jvm_target
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    //自动重命名AAR
    android.libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${project.name}-${defaultConfig.versionName}.aar"
        }
    }
}

tasks.configureEach { task ->
    if (task.name.matches("\\w*compile\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
    if (task.name.matches("\\w*kaptGenerateStubs\\w*Kotlin")) {
        task.dependsOn('greendao')
    }

    if (task.name.matches("\\w*kapt\\w*Kotlin")) {
        task.dependsOn('greendao')
    }
}

//数据库版本以及Dao的相关设置
greendao {
    //数据库schema版本
    schemaVersion 1
    //设置DaoMaster DaoSession Dao包名，默认的是bean所在的包名
    daoPackage 'com.z.db.greendao.dao'
    //设置DaoMaster DaoSeeeion Dao目录，默认是build/generated/source/greendao
    targetGenDir 'src/main/java'
    //设置true,自动生成测试单元
    //generateTests 'true'
    //设置生成的单元测试目录，默认是src/androidTest/java
    //targetGenDirTests 'src/androidTest/java'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //https://developer.android.com/jetpack/androidx/releases/appcompat
    implementation "androidx.appcompat:appcompat:$appcompat_version"

    //Test
    testImplementation "junit:junit:$junit_version"
    androidTestImplementation "androidx.test.ext:junit:$junit_ext_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$test_espresso_version"

    api "org.greenrobot:greendao:$green_dao_version"
}
